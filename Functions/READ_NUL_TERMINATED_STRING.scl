FUNCTION "READ_NUL_TERMINATED_STRING" : Void
TITLE = READ NUL TERMINATED STRING
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : MarvinMangold
FAMILY : SQL
NAME : '1'
VERSION : 0.1
//Strings that are terminated by a [00] byte
   VAR_INPUT 
      data : Array[*] of Byte;   // data where the integer is stored
   END_VAR

   VAR_OUTPUT 
      str : String;   // string
      error : Bool;   // error array data to small
   END_VAR

   VAR_IN_OUT 
      start : Int;   // start index of integer bytes in data
   END_VAR

   VAR_TEMP 
      arrayUp : DInt;
   END_VAR


BEGIN
	(*
	strings that are terminated by a B#16#00 byte
	possible number of bytes is 254
	*)
	
	#arrayUp := UPPER_BOUND(ARR := #data, DIM := 1); // get array upper bounds
	#str := ''; //initialise string
	
	// concat char to string as long as array is big enough and string < 254
	WHILE #start <= #arrayUp AND LEN(#str) < 254 DO
	    // if B#16#00 is found, break loop
	    IF #data[#start] <> B#16#00 THEN
	        #str := CONCAT_STRING(IN1:=#str, IN2:=BYTE_TO_CHAR(#data[#start]));
	        #start += 1;
	    ELSE
	        #start += 1;
	        EXIT;
	    END_IF;
	END_WHILE;
	
	IF #start > #arrayUp THEN
	    #error := True;
	ELSE
	    #error := False;
	END_IF;
END_FUNCTION

